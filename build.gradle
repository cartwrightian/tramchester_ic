
plugins {
    id 'com.mgd.core.gradle.s3' version '2.0.1'
    id 'io.mateo.cxf-codegen' version '2.3.0'
    id 'java'
    id 'application'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

idea{
    module{
        inheritOutputDirs = true
    }
}

import com.mgd.core.gradle.*
import io.mateo.cxf.codegen.wsdl2java.Wsdl2Java

mainClassName = 'com.tramchester.App'

def candidate = System.getenv("CIRCLE_BUILD_NUM") ?: "0"
// override build number if RELEASE_NUMBER set, needed because of way circle ci workflows operate, see also
// .circleci/config.yml
def buildNumber = System.getenv("RELEASE_NUMBER") ?: candidate
def environment = System.getenv("ENV") ?: "Dev"
def isWindows = System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')

// NOTE: TestEnv also depends on the environment

def agentPath = '-agentpath:/home/ianc/dev/tools/visualvm_217/visualvm/lib/deployed/jdk16/linux-amd64/libprofilerinterface.so=/home/ianc/dev/tools/visualvm_217/visualvm/lib,5140'

def s3prefix = 'dist/' + buildNumber
def prodConfig = 'config/local.yml'
def railCodeGenOutput = 'main/src/generated-live-rail-api/'
def railLiveDataWSDL = 'config/OpenLDBWS.wsdl'

s3 {
    bucket = 'tramchesternewdist'
}

// this is used in the generated dist zip filename
version = buildNumber

ext {
    dropwizardVersion = '4.0.7'
    jacksonVersion = '2.17.0'
    awsSdkVersion = '2.25.21'
}

repositories {
    maven {
        url "https://repo.osgeo.org/repository/release/"
    }
    mavenCentral()
    maven {
        url "https://raw.github.com/neo4j-contrib/m2/master/releases"
    }
}

// useful reminder ./gradlew -q dependencyInsight --dependency X.Y:Z
dependencies {

    implementation("io.dropwizard:dropwizard-core:$dropwizardVersion")
    // slf4j in governator was causing race condition with dropwizard start up code on initial get of logger
    implementation('com.netflix.governator:governator:1.17.13') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    implementation 'com.google.inject:guice:7.0.0'
    // note: exclude of org.glassfish.jersey.inject needed as it clashed with version in DropWizard causing a
    // failure to load the DynamicServiceLocator in glassfish
    implementation('org.neo4j:neo4j:5.18.1') {
        // seem to cause issue with logging on every release, sigh
        exclude group: 'org.neo4j', module:'neo4j-slf4j-provider'
        exclude group: 'org.glassfish.jersey.inject'   // , module: 'jersey-hk2'}
        exclude group: 'org.glassfish.jersey.containers', module: 'jersey-container-servlet'
        exclude group: 'org.eclipse.jetty' }
    implementation 'com.github.cliftonlabs:json-simple:4.0.1',
            'commons-io:commons-io:2.16.0',
            'commons-codec:commons-codec:1.16.1',
            'org.apache.commons:commons-collections4:4.4',
            'org.apache.commons:commons-compress:1.26.1',
            'com.github.ben-manes.caffeine:caffeine:3.1.8'
    implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion",
            "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
            "com.fasterxml.jackson.module:jackson-module-afterburner:$jacksonVersion",
            "com.fasterxml.jackson.module:jackson-module-blackbird:$jacksonVersion",
            "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:$jacksonVersion",
            "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jacksonVersion"
    implementation('com.smoketurner:dropwizard-swagger:4.0.5-1') {
        exclude group: 'org.glassfish.jersey.containers', module: 'jersey-container-servlet-core'
        exclude group: 'org.glassfish.jersey.core', module: 'jersey-server'
        exclude group: 'org.glassfish.jersey.core', module: 'jersey-common'
    }
    implementation 'org.reflections:reflections:0.10.2'

    // aws sdk
    implementation("software.amazon.awssdk:s3:$awsSdkVersion")
    implementation("software.amazon.awssdk:cloudwatch:$awsSdkVersion")
    implementation("software.amazon.awssdk:cloudformation:$awsSdkVersion")
    implementation("software.amazon.awssdk:sns:$awsSdkVersion")
    implementation("software.amazon.awssdk:sqs:$awsSdkVersion")

    // todo replace with jakarta version, but used by governator and cxf code gen
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    implementation  'org.jetbrains:annotations:24.1.0'

    implementation('org.geotools:gt-main:30.2',
            'org.geotools:gt-epsg-hsql:30.2')
    implementation('javax.measure:unit-api:2.2')


    implementation('org.roaringbitmap:RoaringBitmap:1.0.5')

    testImplementation("io.dropwizard:dropwizard-testing:$dropwizardVersion")
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2',
            'org.junit.platform:junit-platform-launcher:1.10.2',
            'org.assertj:assertj-core:3.25.3',
            'org.easymock:easymock:5.2.0',
            'org.seleniumhq.selenium:selenium-java:4.21.0',
            'org.hamcrest:hamcrest:2.2'

    // cxf complains about missing conduit without this
    cxfCodegen 'org.apache.cxf:cxf-rt-transports-http:4.0.4'
    // needed for cxf gen'ed code, missing from cxfCodegen dependencies?
    implementation 'jakarta.xml.ws:jakarta.xml.ws-api:4.0.1'
    // for running the cxf generated code,  provide implementation of the SPI jakarta.xml.ws.spi.Provider
    runtimeOnly 'com.sun.xml.ws:jaxws-rt:4.0.2'

    // TODO Still needed? Workaround for issues running tests in intellij and glassfish DI, does not occur with running via gradle
    //testRuntimeOnly 'javax.servlet:javax.servlet-api:3.0.1'

    // NOTE: get resources files generated by webpack into path for intellij at test time, but this workaround is not working
    //testRuntimeOnly files('build/resources/main/app') // build/resources/main/app => index.html (found)
    //testRuntimeOnly fileTree(dir: 'build/resources/main/', include: '*.*')
}

sourceSets {
    main {
        java {
            srcDirs = ['main/src']
        }
        resources {
            //srcDirs = ['main/src/resources','config']
            srcDirs = ['config']
        }
    }
    test {
        java {
            srcDirs = ['main/test']
        }
        resources {
            srcDirs = ['main/test/resources']
        }
    }
}

cxfCodegen {
    cxfVersion = "4.0.4"
}

tasks.register("railLiveDataCodeGen", Wsdl2Java) {
    toolOptions {
        wsdl.set(file(railLiveDataWSDL).toPath().toAbsolutePath().toString())
        outputDir.set(file(railCodeGenOutput))
        markGenerated.set(true)
        extendedSoapHeaders.set(true)
    }
}

distZip {
    dependsOn 'queryLiveDataArchiveStartScript'
}

tasks.register('npmInstall', Exec) {
    inputs.file("package.json")
    inputs.file("package-lock.json")
    outputs.dir("node_modules")

    if (isWindows) {
        commandLine "npm.cmd", "install"
    } else {
        commandLine "npm", "install"
    }
}

tasks.register('webpack', Exec) {
    dependsOn 'npmInstall'
    inputs.file("package.json")
    inputs.file("package-lock.json")
    inputs.file("webpack.config.js")
    inputs.files(fileTree("main/src/resources/app"))

    if (isWindows) {
        commandLine './node_modules/.bin/webpack.cmd', '--env', 'environment='+environment
    } else {
        commandLine './node_modules/.bin/webpack',  '--env', 'environment='+environment
    }
}

// todo is this correct see next task
applicationDistribution.exclude('**/app/')

// TODO this does not seem  the right way to do this
jar {
    dependsOn 'webpack'
    from('build/resources/main') {
        duplicatesStrategy duplicatesStrategy.INCLUDE
        exclude '**.yml'
    }
    manifest {
        attributes 'Main-Class': 'com.tramchester.App'
    }
}

distributions {
    main {
        contents {
            into ('config') {
                from 'config'
            }
            into ('bin') {
                from 'deploy/start.sh'
            }
        }
    }
}

// WIP removed, problem with runtime classpath for the apache tool that only happened with circleci build env
compileJava.dependsOn 'railLiveDataCodeGen'

tasks.register('cfnassist') {
    doLast {
        ant.taskdef(name: 'cfnassist', classname: 'tw.com.ant.CfnAssistAntTask') {
            classpath {
                fileset(dir: 'lib/cfnassist-1.3.182/lib/', includes: '*.jar')
                dirset(dir: 'lib/cfnassist-1.3.182/conf/')
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.deprecation = true
    options.compilerArgs.addAll(['-Xlint'])
}

tasks.register('cleanGraph') {
    fileTree('databases').
            include ('**/*.db').
            visit { FileVisitDetails  details -> delete details.file }
}

tasks.register('cleanRailCode') {
    delete fileTree(railCodeGenOutput) {
        include '**.*'
    }
    delete railCodeGenOutput
}

//// TODO Sort this out!
clean {
    dependsOn 'cleanGraph', 'cleanRailCode'
    delete fileTree('data/tram') {
        include '*.txt'
        include '*.zip'
        include '*.csv'
    }

    delete fileTree('data/bus') {
        include '*.txt'
        include '*.zip'
        include '*.csv'
    }
    delete fileTree('data/rail') {
        include '*.*'
    }
    delete fileTree('data/naptan') {
        include '*.xml'
        include '*.zip'
    }
    delete fileTree('data/nptg') {
        include '*.csv'
        include '*.zip'
    }
    delete fileTree('data/postcodes') {
        include '**/*.csv'
    }
    delete('data/postcodes')
    delete('testData/cache')
}

run {
    // see deploy/setupTramWebServerAWSLinux.sh
    //maxHeapSize = '900m'
    dependsOn 'webpack'
    args 'server', prodConfig
}

test {
    useJUnitPlatform()
}

tasks.register('cdkApp', JavaExec) {
    dependsOn classes
    mainClass = 'com.tramchester.deployment.CdkApp'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('buses', JavaExec) {
    dependsOn 'webpack'
    mainClass = mainClassName
    classpath = sourceSets.main.runtimeClasspath
//    maxHeapSize = "3g"
    args 'server', 'config/buses.yml'
}

tasks.register('frequency', JavaExec) {
    dependsOn 'webpack'
    mainClass = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    maxHeapSize = "3g"
    args 'server', 'config/frequency.yml'
}

tasks.register('rail', JavaExec) {
    dependsOn 'webpack'
    mainClass = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    maxHeapSize = "3g"
    args 'server', 'config/rail.yml'
}

tasks.register('gm', JavaExec) {
    dependsOn 'webpack'
    mainClass = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    maxHeapSize = "2g"
    args 'server', 'config/gm.yml'
}

tasks.register('gmProfile', JavaExec) {
    dependsOn 'webpack'
    mainClass = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    maxHeapSize = "1g"
    jvmArgs agentPath
    args 'server', 'config/gm.yml'
}

tasks.register('all', JavaExec) {
    dependsOn 'webpack'
    mainClass = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    maxHeapSize = "3g"
    args 'server', 'config/all.yml'
}

tasks.register('busTest', Test) {
    useJUnitPlatform {
        includeTags 'BusTest'
    }
}

tasks.register('unit', Test) {
    dependsOn 'compileJava'
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.unit.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
    }
}

tasks.register('liveMessages', Test) {
    dependsOn 'compileJava'
    useJUnitPlatform {
        includeTags "liveDataMessages"
        includeTags "liveData"
        excludeTags "LiveDataS3UploadTest"
    }
    filter {
        includeTestsMatching "com.tramchester.integration.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError"
    }
}

def integrationTestConfiguration = {
    minHeapSize = "1000m"
    maxHeapSize = "1750m"
    useJUnitPlatform {
        // excludeTags "liveDataMessages" // these go away periodically...
        //excludeTags "liveData" // also goes away on occasion
        //excludeTags "dataExpiry" // can be useful to exclude when big changes to timetable are upcoming
        //excludeTags 'liveDataDueTrams' // tfgm api has know issue
        excludeTags "postcode" // not needed for tram, but used by buses
        excludeTags "BusTest"
        excludeTags "TrainTest"
        excludeTags "GMTest"
        excludeTags "LiveDataS3UploadTest"
        excludeTags "AWSMetricsTest"
    }
    filter {
        includeTestsMatching "com.tramchester.integration.*"
    }
    testLogging {
        events "passed", "failed", "standardError"
    }
}

tasks.register('integration', Test) {
    dependsOn 'compileJava'
    configure integrationTestConfiguration
}

tasks.register('integrationProfile', Test) {
    dependsOn 'compileJava'
    configure integrationTestConfiguration
    jvmArgs agentPath
}

tasks.register('integrationGM', Test) {
    dependsOn 'compileJava'
    minHeapSize = "1000m"
    maxHeapSize = "2000m"
    useJUnitPlatform {
        includeTags "GMTest"
        includeTags "dataExpiry"
    }
    filter {
        includeTestsMatching "com.tramchester.integration.*"
    }
    //systemProperty("com.tramchester.config", "RailAndTramGreaterManchesterConfig")
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "failed", "standardError"
    }
}

tasks.register('dualTestIntegration', Test) {
    dependsOn 'compileJava'
    minHeapSize = "1000m"
    maxHeapSize = "1750m"
    useJUnitPlatform {
        includeTags "DualTest"
    }
    filter {
        includeTestsMatching "com.tramchester.integration.*"
    }
    systemProperty("com.tramchester.config", "RailAndTramGreaterManchesterConfig")
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "failed", "standardError" //, "standardOut"
    }
}

tasks.register('dataUpdated', Test) {
    configure integrationTestConfiguration
    outputs.upToDateWhen { false }
    useJUnitPlatform {
        includeTags "DataUpdateTest"
    }
}

tasks.register('s3Test', Test) {
    dependsOn 'compileJava'
    useJUnitPlatform {
        includeTags "S3Test"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "failed", "standardError"
    }
}

tasks.register('integrationBus', Test) {
    dependsOn 'compileJava'
    minHeapSize = "1000m"
    maxHeapSize = "3000m"
    useJUnitPlatform {
        includeTags "BusTest"
        //includeTags "postcode"
    }
    filter {
        includeTestsMatching "com.tramchester.integration.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError" //, "standardOut"
    }
}

def integrationRailConfig = {
    dependsOn 'compileJava'
    minHeapSize = "1000m"
    maxHeapSize = "8000m"
    useJUnitPlatform {
        includeTags "TrainTest"
    }
    systemProperty("com.tramchester.config", "IntegrationRailTestConfig")
    filter {
        includeTestsMatching "com.tramchester.integration.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "failed", "standardError" //, "standardOut"
    }
}

tasks.register('integrationRail', Test) {
    configure integrationRailConfig
}

tasks.register('integrationRailProfile', Test) {
    configure integrationRailConfig
    jvmArgs agentPath
}

tasks.register('graphTests', Test) {
    dependsOn 'compileJava'
    minHeapSize = "1000m"
    maxHeapSize = "1750m"
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.integration.graph.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError" //, "standardOut"
    }
}

tasks.register('allStationsTest', Test) {
    dependsOn 'compileJava'
    outputs.upToDateWhen { false }
    minHeapSize = "1000m"
    maxHeapSize = "1750m"
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.integration.graph.RouteCalculatorTestAllJourneys"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events("passed", "skipped", "failed")
    }
}

tasks.register('buildGraphTest', Test) {
    dependsOn 'compileJava'
    minHeapSize = "1000m"
    maxHeapSize = "1750m"
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.integration.graph.GraphBuildAndStartTest"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started" //, "standardError", "standardOut"
    }
}

def acceptanceTestConfiguration = {
    dependsOn 'compileJava'
    dependsOn 'webpack'
    systemProperty("user.language", "en")
    systemProperty("user.country", "GB")
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.acceptance.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError", "standardOut"
    }
}

tasks.register('acceptance', Test) {
//    dependsOn 'allowhost'
    configure acceptanceTestConfiguration
//    finalizedBy {
//        blockhost
//    }
}

tasks.register('localAcceptance', Test) {
    minHeapSize = "1000m"
    maxHeapSize = "1750m"
    configure acceptanceTestConfiguration
}

// fast feedback on whether acceptance tests run etc
tasks.register('smokeTest', Test) {
    configure acceptanceTestConfiguration
    useJUnitPlatform {
        includeTags "SmokeTest"
    }
}

// aid with debuggin locale issue quickly
tasks.register('localeTest', Test) {
    configure acceptanceTestConfiguration
    useJUnitPlatform {
        includeTags "LocaleTest"
    }
}

tasks.register('diagrams') {
    dependsOn 'cfnassist'
    doLast {
        ant.cfnassist() {
            Diagrams(target: '.')
        }
    }
}

// gm here so include both sets of data
tasks.register('pullData', JavaExec) {
    dependsOn 'classes'
    mainClass = 'com.tramchester.deployment.cli.FetchDataCLI'
    classpath = sourceSets.main.runtimeClasspath
    args prodConfig
}

tasks.register('publishLiveData', JavaExec) {
    dependsOn 'classes'
    mainClass = 'com.tramchester.deployment.cli.PublishLiveDataToSNSCLI'
    classpath = sourceSets.main.runtimeClasspath
    args 'config/local.yml', '100'
}

// gm here so include both sets of data
tasks.register('queryDueTramStatus', JavaExec) {
    dependsOn 'classes'
    mainClass = 'com.tramchester.deployment.cli.QueryLiveDataArchiveCLI'
    classpath = sourceSets.main.runtimeClasspath
    args '2023-01-30', '3', '15', 'config/gm.yml', 'dueTramStatus.txt'
}

tasks.register('queryLiveDataArchiveStartScript', CreateStartScripts) {
    dependsOn 'compileJava'
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
    mainClass = 'com.tramchester.deployment.cli.QueryLiveDataArchiveCLI'
    applicationName = 'queryLiveData'
}

// temp additional target while deploying train/tram version to just UAT
//tasks.register('buildDatabaseUAT', JavaExec) {
//    dependsOn classes
//    mainClass = 'com.tramchester.deployment.cli.BuildGraphCLI'
//    classpath = sourceSets.main.runtimeClasspath
//    maxHeapSize = "2g"
//    args 'config/gm.yml', 'build/distributions/database'
//}

// todo re-instate UAT at some stage, database zip's for different config will be needed
tasks.register('buildDatabase', JavaExec) {
    dependsOn 'classes'
    dependsOn 'pullData'
    //dependsOn buildDatabaseUAT
    mainClass = 'com.tramchester.deployment.cli.BuildGraphCLI'
    classpath = sourceSets.main.runtimeClasspath
    args prodConfig //, 'build/distributions/database'
}

// gm here so include both sets of data
tasks.register('uploadDataSources', JavaExec) {
    dependsOn 'pullData'
    dependsOn 'classes'
    dependsOn 'buildDatabase'
    mainClass = 'com.tramchester.deployment.cli.UploadSourceDataCLI'
    classpath = sourceSets.main.runtimeClasspath
    args prodConfig, s3prefix
}

distZip {
    dependsOn 'queryLiveDataArchiveStartScript'
    dependsOn 'webpack'
}

distTar.dependsOn('webpack')

tasks.register('copyIntoUploadDir', Copy) {
    dependsOn 'distZip'
    dependsOn 'classes'
    from 'build/distributions/tramchester-' + buildNumber + '.zip'
    from 'deploy/setupTramWebServerAWSLinux.sh'
    from 'deploy/cloudInitAWSLinux.txt'
    into 'build/distributions/uploadDir'
}

tasks.register('uploadApp', S3Upload) {
    dependsOn 'copyIntoUploadDir'
    keyPrefix = s3prefix
    sourceDir = 'build/distributions/uploadDir'
}

tasks.register('upload') {
    dependsOn 'uploadDataSources'
    dependsOn 'uploadApp'
}


// >>>>> https://data.atoc.org/member-area

tasks.register('uploadRailToS3', S3Upload) {
    key = 'railData/ttis928.zip'
    file = 'data/rail/ttis928.zip'
    overwrite = false
}

tasks.register('infra') {
    dependsOn 'cfnassist'
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment, sns: 'true', capabilityIAM: 'true') {
            Templates(target: 'deploy/infra/' + environment)
        }
    }
}

tasks.register('deploy') {
    dependsOn 'cfnassist'
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment, buildNumber: buildNumber, sns: 'true') {
            Templates(target: 'deploy/servers.json')
        }
    }
}

tasks.register('undeploy') {
    dependsOn 'cfnassist'
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment, buildNumber: buildNumber, sns: 'true') {
            Delete(target: 'deploy/servers.json')
        }
    }
}

tasks.register('updateLB') {
    dependsOn 'cfnassist'
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment, buildNumber: buildNumber, sns: 'true') {
            TargetGroupUpdate(typeTag: 'web', port: '8080',)
        }
    }
}

tasks.register('tidy') {
    dependsOn 'cfnassist'
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment) {
            TidyStacks(target: 'deploy/servers.json', typeTag: 'web')
        }
    }
}

//tasks.register('allowHostDev') {
//    dependsOn 'cfnassist'
//    doLast {
//        ant.cfnassist(project: 'tramchesterB', env: 'Dev') {
//            WhiteList(port: '443', tag: 'web')
//        }
//    }
//}
//
//tasks.register('blockHostDev') {
//    dependsOn 'cfnassist'
//    doLast {
//        ant.cfnassist(project: 'tramchesterB', env: 'Dev') {
//            BlackList(port: '443', tag: 'web')
//        }
//    }
//}
//
//tasks.register('allowHostUAT') {
//    dependsOn 'cfnassist'
//    doLast {
//        ant.cfnassist(project: 'tramchesterB', env: 'UAT') {
//            WhiteList(port: '443', tag: 'web')
//        }
//    }
//}
//
//tasks.register('denyHostUAT') {
//    dependsOn 'cfnassist'
//    doLast {
//        ant.cfnassist(project: 'tramchesterB', env: 'UAT') {
//            BlackList(port: '443', tag: 'web')
//        }
//    }
//}
//
//tasks.register('allowhost') {
//    dependsOn 'cfnassist'
//    doLast {
//        ant.cfnassist(project: 'tramchesterB', env: environment) {
//            WhiteList(port: '443', tag: 'web')
//        }
//    }
//}
//
//tasks.register('blockhost') {
//    dependsOn 'cfnassist'
//    doLast {
//        ant.cfnassist(project: 'tramchesterB', env: environment) {
//            BlackList(port: '443', tag: 'web')
//        }
//    }
//}
